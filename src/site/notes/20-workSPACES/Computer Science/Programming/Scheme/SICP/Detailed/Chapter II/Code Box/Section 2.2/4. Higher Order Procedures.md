---
{"dg-publish":true,"permalink":"/20-work-spaces/computer-science/programming/scheme/sicp/detailed/chapter-ii/code-box/section-2-2/4-higher-order-procedures/"}
---



up:: 
tags:: 



# Unfold, Filter, Map, Fold

## For Lists

### Template
```scheme
#lang racket

(define (fn-for-lst lst)
  (cond [(empty? lst) (... lst)]
        [else (... (first lst)
                   (fn-for-lst (rest lst)))]))
```

### Enumerate & Unfold
#### Enumerate

```scheme
#lang racket

(define (enumerate start end)
  (cond [(> start end) empty]
        [else (cons start (enumerate (+ start 1) end))]))

(enumerate 1 10) ; '(1 2 3 4 5 6 7 8 9 10)
```

#### Unfold

```scheme
(define (unfold stop? term next initial)
  (let ([val (term initial)])
    (cond [(stop? val) '()]
          [else (cons val (unfold stop? term next (next initial)))])))

(unfold (lambda (v) (> v 10))
        (lambda (v) v)
        (lambda (v) (+ v 1))
        1) ; '(1 2 3 4 5 6 7 8 9 10)
```

### Filter
```scheme
(define (filter pred lst)
  (cond [(empty? lst) lst]
        [else (if (pred (car lst))
                  (cons (car lst) (filter pred  (cdr lst)))
                  (filter pred (cdr lst)))]))


(filter even? (unfold (lambda (v) (> v 10))
                      (lambda (v) v)
                      (lambda (v) (+ v 1))
                      1)) ; '(2 4 6 8 10)
```

### Map

```scheme
(define (map f lst)
  (cond [(empty? lst) empty]
        [else (cons (f (car lst)) (map f (cdr lst)))]))


(map (lambda (v) (* 3 v)) (unfold (lambda (v) (> v 10))
                      (lambda (v) v)
                      (lambda (v) (+ v 1))
                      1)) ; '(3 6 9 12 15 18 21 24 27 30)
```


### Fold
```scheme
(define (fold combiner base lst)
  (cond [(empty? lst) base]
        [else (combiner (car lst)
                        (fold combiner base (cdr lst)))]))


(fold + 0 (unfold (lambda (v) (> v 10))
                      (lambda (v) v)
                      (lambda (v) (+ v 1))
                      1)) ; 55
```


### Pipeline
```scheme
; enumerate -> filter -> map -> fold
(fold + 0 (map (lambda (x) (* 3 x)) (filter odd? (enumerate 1 100))))
```



## For Multi Arity Trees

### Template
```php
#lang racket

(define (fn-for-node subtree)
  (... (node-val subtree)
       (fn-for-children (node-children subtree))))

(define (fn-for-children lon)
  (cond [(empty? lon) (... lon)]
        [else (... (fn-for-node (first lon))
                   (fn-for-children (rest lon)))]))

```

### Enumerate & Unfold
#### Unfold
```scheme
(define (unfold node-value-fn child-seeds-fn initial-seed)
  ;creates a subtree from seed
  (define (build-subtree-from-seed current-seed)
    (make-tree (node-value-fn current-seed) ; current value
               ;map creates a list of subtrees!!
               (map (lambda (child-seed)
                      (build-subtree-from-seed child-seed))
                    ;creates seeds for each subtree
                    (child-seeds-fn current-seed))))
  
  (build-subtree-from-seed initial-seed))
  
(define generated-tree
  (letrec ([node-val-fn (lambda (seed) (if (= (remainder seed 2) 1)
                                           (+ 1 (* 3 seed))
                                           (/ seed 2)))]
           [child-seeds-fn (lambda (seed)
                             (cond [(<= seed 0) empty]
                                   [else (cons (random seed)
                                               (child-seeds-fn (- seed 1)))]))])
    (unfold node-val-fn child-seeds-fn 5)))
    
(pretty-print-folder-style generated-tree)
#| Tree is printed as shown!

+-- 16
|   +-- 4
|   |   L-- 0
|   +-- 4
|   |   L-- 0
|   +-- 1
|   |   +-- 4
|   |   |   L-- 0
|   |   L-- 0
|   +-- 4
|   |   L-- 0
|   L-- 0

|#

```


#### Enumerate
```scheme
; Enumerate
(define (enumerate-tree tree)
  (define (enumerate-node node id)
    (cons (cons (node-val node) id)
          (enumerate-children (node-children node) (+ id 1))))
  (define (enumerate-children lon id)
    (cond [(empty? lon) empty]
          [else (append-lists (enumerate-node (first lon) id)
                     (enumerate-children (rest lon) (+ id 1)))]))
  (enumerate-node tree 0))

#| root is the following tree:

+-- 253
|   +-- 144
|   |   +-- 36
|   |   +-- 48
|   |   L-- 60
|   +-- 37
|   |   +-- 12
|   |   L-- 25
|   L-- 72

|#

(enumerate-tree root) ; '((253 . 0) (144 . 1) (36 . 2) (48 . 3) (60 . 4) (37 . 2) (12 . 3) (25 . 4) (72 . 3))

```

### Map
```scheme
;MAP FOR TREE
(define (map-tree f tree)
  (make-tree (f (node-val tree))
             (map-children f (node-children tree))))

(define (map-children f lon)
  (cond [(empty? lon) empty]
        [else (cons (map-tree f (first lon))
                    (map-children f (rest lon)))]))

(map-tree (lambda (x) (* 3 x)) root) ; '(759 (432 (108) (144) (180)) (111 (36) (75)) (216))
```

### Filter

```scheme
(define (filter pred root)
  (let ([v (node-val root)]
        [fc (filter-children pred (node-children root))])
    (if (pred v)
        (make-tree v fc)
        ; the degenerate case!!! when the removed node has no children to replace it with!!!
        (cond [(empty? fc) empty] 
              [else (let ([newroot (first fc)]
                          [oldchildren (rest fc)])
                      (make-tree (node-val newroot)
                                 (append-lists (node-children newroot) oldchildren)))]))))

(define (filter-children pred lon)
  (cond [(empty? lon) empty]
        [else (let ([ffc (filter pred (first lon))]
                    [frc (filter-children pred (rest lon))])
                (if (empty? ffc) ; we don't want to cons '() to a list of nodes!!!
                    frc
                    (cons ffc frc)))]))


(filter even? root) ; '(144 (36) (48) (60) (12) (72))
#|
+-- 144
|   +-- 36
|   +-- 48
|   +-- 60
|   +-- 12
|   L-- 72
|#

(filter odd? root)

#|
+-- 253
|   L-- 37
|       L-- 25
|#

(filter even? (make-node 13 empty)) ; '()
```

### Fold

```scheme
; FOLD for TREE:

(define (fold c1 c2 base tree)
  (c1 (node-val tree)
      (fold-children c1 c2 base (node-children tree))))

(define (fold-children c1 c2 base lon)
  (cond [(empty? lon) base]
        [else (c2 (fold c1 c2 base (first lon))
                  (fold-children c1 c2 base (rest lon)))]))

(+ 253 144 36 48 60 37 12 25 72)
(fold + + 0 root)
```


### Pipeline
```scheme
;; Conventional Interface Style
(letrec ([node-val-fn (lambda (seed) (if (= (remainder seed 2) 1)
                                         (+ 1 (* 3 seed))
                                         (/ seed 2)))]
         [child-seeds-fn (lambda (seed)
                           (cond [(<= seed 0) empty]
                                 [else (cons (random seed)
                                             (child-seeds-fn (- seed 1)))]))]
         [generated-tree (unfold node-val-fn child-seeds-fn 7)]
         [filtered-tree (filter even? generated-tree)]
         [mapped-tree (map-tree (lambda (v) (+ 2 (* 3 v))) filtered-tree)])
  (displayln "STARTS THE CONVENTIONAL TREE INTERFACE OPERATIONS!!!")
  (pretty-print-folder-style generated-tree)
  (displayln "||\n||\n\\/\n")
  (pretty-print-folder-style filtered-tree)
  (displayln "||\n||\n\\/\n")
  (pretty-print-folder-style mapped-tree)
  (displayln "||\n||\n\\/\n")
  (fold + + 0 mapped-tree))
```

### Total 
```scheme
#lang racket


; Node is compound (v ListofNode)

; ListOfNode is one of:
; - empty
; - (cons Node ListOfNode)

; Self Reference -> Breadth
; Mutual Reference -> Node -> ListofNode -> Node (Depth)


(define (make-node val children) (cons val children))
(define (node-val node) (car node))
(define (node-children node) (cdr node))



(define (make-list node) (cons node empty))
(define (prepend node lon) (cons node lon))
(define (first lon) (car lon))
(define (rest lon) (cdr lon))


(define (make-tree v children) (make-node v children))
(define (leaf? tree) (empty? (node-children tree)))


(define (count-leaves tree)
  (if (leaf? tree)
      1
      (count-subs (node-children tree))))

(define (count-subs lon)
  (cond [(empty? lon) 0]
        [else (+   (count-leaves (first lon))
                   (count-subs (rest lon)))]))

(define (append-lists l1 l2)
  (cond [(empty? l1) l2]
        [(empty? l2) l1]
        [else (cons (car l1) (append-lists (cdr l1) l2))]))

(define (pre-order tree)
  (cons (node-val tree)
        (pre-traverse (node-children tree))))

(define (pre-traverse lon)
  (cond [(empty? lon) empty]
        [else (append-lists (pre-order (first lon))
                            (pre-traverse (rest lon)))]))


(define (post-order tree)
  (append-lists (post-traverse (node-children tree)) (list (node-val tree))))

(define (post-traverse lon)
  (cond [(empty? lon) empty]
        [else (append-lists (post-order (first lon))
                            (post-traverse (rest lon)))]))


(define NODE12 (make-node 12 empty))
(define NODE25 (make-node 25 empty))
(define NODE36 (make-node 36 empty))
(define NODE48 (make-node 48 empty))
(define NODE60 (make-node 60 empty))
(define NODE72 (make-node 72 empty))

(define NODE37 (make-node 37 (list NODE12 NODE25)))
(define NODE144 (make-node 144 (list NODE36 NODE48 NODE60)))
(define NODE253 (make-node 253 (list NODE144 NODE37 NODE72)))

(define root NODE253)

; Enumerate
(define (enumerate-tree tree)
  (define (enumerate-node node id)
    (cons (cons (node-val node) id)
          (enumerate-children (node-children node) (+ id 1))))
  (define (enumerate-children lon id)
    (cond [(empty? lon) empty]
          [else (append-lists (enumerate-node (first lon) id)
                     (enumerate-children (rest lon) (+ id 1)))]))
  (enumerate-node tree 0))

(enumerate-tree root) ; '((253 . 0) (144 . 1) (36 . 2) (48 . 3) (60 . 4) (37 . 2) (12 . 3) (25 . 4) (72 . 3))


;MAP FOR TREE
(define (map-tree f tree)
  (make-tree (f (node-val tree))
             (map-children f (node-children tree))))

(define (map-children f lon)
  (cond [(empty? lon) empty]
        [else (cons (map-tree f (first lon))
                    (map-children f (rest lon)))]))


(map-tree (lambda (x) (* 3 x)) root)

; FILTER FOR TREE:

(define (filter pred root)
  (let ([v (node-val root)]
        [fc (filter-children pred (node-children root))])
    (if (pred v)
        (make-tree v fc)
        (cond [(empty? fc) empty] ; the degenerate case!!! when the removed node has no children to replace it with!!!
              [else (let ([newroot (first fc)]
                          [oldchildren (rest fc)])
                      (make-tree (node-val newroot)
                                 (append-lists (node-children newroot) oldchildren)))]))))

(define (filter-children pred lon)
  (cond [(empty? lon) empty]
        [else (let ([ffc (filter pred (first lon))]
                    [frc (filter-children pred (rest lon))])
                (if (empty? ffc) ; we don't want to cons '() to a list of nodes!!!
                    frc
                    (cons ffc frc)))]))


(filter even? root) ; '(144 (36) (48) (60) (12) (72))
(filter even? (make-node 13 empty)) ; '()

; FOLD for TREE:

(define (fold c1 c2 base tree)
  (c1 (node-val tree)
      (fold-children c1 c2 base (node-children tree))))

(define (fold-children c1 c2 base lon)
  (cond [(empty? lon) base]
        [else (c2 (fold c1 c2 base (first lon))
                  (fold-children c1 c2 base (rest lon)))]))

(+ 253 144 36 48 60 37 12 25 72)
(fold + + 0 root)

; UNFOLD FOR TREE
(define (unfold node-value-fn child-seeds-fn initial-seed)
  ;creates a subtree from seed
  (define (build-subtree-from-seed current-seed)
    (make-tree (node-value-fn current-seed) ; current value
               ;children made of nodes!!
               (map (lambda (child-seed)
                      (build-subtree-from-seed child-seed))
                    ;creates seeds for each subtree
                    (child-seeds-fn current-seed))))
  
  (build-subtree-from-seed initial-seed))


(define generated-tree
  (letrec ([node-val-fn (lambda (seed) (if (= (remainder seed 2) 1)
                                           (+ 1 (* 3 seed))
                                           (/ seed 2)))]
           [child-seeds-fn (lambda (seed)
                             (cond [(<= seed 0) empty]
                                   [else (cons (random seed)
                                               (child-seeds-fn (- seed 1)))]))])
    (unfold node-val-fn child-seeds-fn 5)))




;; Pretty Print Folder Style

(define (pretty-print-folder-style tree)
  (define (print-children lon indent-prefix)
    (cond [(empty? lon) (display "")]
          [else (let ([child (first lon)]
                      [is-last  (empty? (cdr lon))])
                  (print-node child indent-prefix is-last)
                  (print-children (rest lon) indent-prefix))]))
  
  (define (print-node cnode current-prefix is-last-sibling)
    (let ([connector (if is-last-sibling  "L-- " "+-- ")])
      (display (string-append current-prefix
                              connector
                              (format "~a" (node-val cnode))))
      (newline)
      (print-children  (node-children cnode)
                       (string-append current-prefix
                                      (if is-last-sibling "    " "|   ")))))

  (print-node tree "" (empty? (node-children tree)))
  (newline))

(pretty-print-folder-style root)
(pretty-print-folder-style generated-tree)


;; Conventional Interface Style

(letrec ([node-val-fn (lambda (seed) (if (= (remainder seed 2) 1)
                                         (+ 1 (* 3 seed))
                                         (/ seed 2)))]
         [child-seeds-fn (lambda (seed)
                           (cond [(<= seed 0) empty]
                                 [else (cons (random seed)
                                             (child-seeds-fn (- seed 1)))]))]
         [generated-tree (unfold node-val-fn child-seeds-fn 7)]
         [filtered-tree (filter even? generated-tree)]
         [mapped-tree (map-tree (lambda (v) (+ 2 (* 3 v))) filtered-tree)])
  (displayln "STARTS THE CONVENTIONAL TREE INTERFACE OPERATIONS!!!")
  (pretty-print-folder-style generated-tree)
  (displayln "||\n||\n\\/\n")
  (pretty-print-folder-style filtered-tree)
  (displayln "||\n||\n\\/\n")
  (pretty-print-folder-style mapped-tree)
  (displayln "||\n||\n\\/\n")
  (fold + + 0 mapped-tree))


```


---

## ðŸ”‘ Key Points
- 
## â“ Questions
- 
## ðŸ“¦ Resources
- 
## ðŸŽ¯ Actions
- [ ] 
- [ ] 
- [ ] 
- [ ] 
- [ ] 